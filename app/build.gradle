plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Đọc version từ file properties
def versionPropsFile = file('../version.properties')
def versionProps = new Properties()
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

def versionNameProp = versionProps['VERSION_NAME'] ?: '1.0_txa'
def versionCodeProp = versionProps['VERSION_CODE'] ?: '1'

android {
    namespace 'com.txahub.vn'
    compileSdk 34

    defaultConfig {
        applicationId "com.txahub.vn"
        minSdk 24
        targetSdk 34
        versionCode versionCodeProp.toInteger()
        versionName versionNameProp

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Build variants: mobile và pc
    flavorDimensions "device"
    productFlavors {
        mobile {
            dimension "device"
            applicationIdSuffix ""
            versionNameSuffix ""
            resValue "string", "app_name", "TXA Hub"
        }
        pc {
            dimension "device"
            applicationIdSuffix ".pc"
            versionNameSuffix "_pc"
            resValue "string", "app_name", "TXA Hub PC"
            // PC có thể cần minSdk cao hơn cho desktop features
            minSdk 26
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.webkit:webkit:1.9.0'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

